/*
** Copyright (C) 2011, 2015  International Business Machines Corporation
** All Rights Reserved
*/

namespace sample;

use com.ibm.streamsx.network.ipv4::*;
use com.ibm.streamsx.network.ipv6::*;
use com.ibm.streamsx.network.source::*;

composite TestPacketFileSourceBasic1 {

	param		
	expression<rstring> $pcapFilename: getSubmissionTimeValue("pcapFilename", "../../SampleNetworkToolkitData/sample_dns+dhcp.pcap" );
	
	type 
	
	PacketType =
		uint64 packetNumber,			// sequence number of packet, as emitted by operator
	    uint64 packetsProcessed, 		// total number of packets processed by operator, so far
	    uint64 octetsProcessed,			// total number of octets of packet data processed by operator, so far
		float64 captureTime,			// time that packet was captured, in seconds since Unix epoch 
	    uint8 ipVersion,				// IP version: 4 for IPv4, 6 for IPv6
	    rstring ipProtocol,				// IP protocol: 0x01==1 for ICMP, 0x6==6 for TCP, 0x11==17 for UDP
	    rstring ipSourceAddress,		// IP source address, or empty if not IP packet
	    uint16 ipSourcePort,			// IP source port, or zero if not UDP or TCP packet
	    rstring ipDestinationAddress,	// IP destination address, or empty if not IP packet
	    uint16 ipDestinationPort,		// IP destination port, or zero if not UDP or TCP packet
		uint32 packetLength,			// original length of packet (not necessarily all captured)
		uint32 payloadLength,			// length of packet payload, following all network headers
		blob packetData,				// contents of packet captured, including network headers (possibly truncated)
		blob payloadData;				// contents of packet payload, following all network headers

	graph

	stream<PacketType> PacketStream as OutPackets = PacketFileSource() {
	    logic state: { map<uint8,rstring> ipProtocols = { 1: "icmp", 6: "tcp", 17: "udp" }; } 
		param
			pcapFilename: $pcapFilename;
		output OutPackets:
			packetNumber = packetsProcessed() - 1ul,
			packetsProcessed = packetsProcessed(),
			octetsProcessed = octetsProcessed(),
			captureTime = (float64)CAPTURE_SECONDS() + (float64)CAPTURE_MICROSECONDS() / 1000000.0,
			ipVersion = IP_VERSION(),
			ipProtocol = IP_PROTOCOL() in ipProtocols ? ipProtocols[IP_PROTOCOL()] : (rstring)IP_PROTOCOL(),
			ipSourceAddress = IP_VERSION()==4ub ? convertIPV4AddressNumericToString(IPV4_SRC_ADDRESS()) : ( IP_VERSION()==6ub ? convertIPV6AddressNumericToString(IPV6_SRC_ADDRESS()) : ""),
			ipDestinationAddress = IP_VERSION()==4ub ? convertIPV4AddressNumericToString(IPV4_DST_ADDRESS()) : ( IP_VERSION()==6ub ? convertIPV6AddressNumericToString(IPV6_DST_ADDRESS()) : ""),
			ipSourcePort = IP_SRC_PORT(),
			ipDestinationPort = IP_DST_PORT(),
		    packetLength = PACKET_LENGTH(),
			payloadLength = PAYLOAD_LENGTH(),
			packetData = PACKET_DATA(),
			payloadData = PAYLOAD_DATA();
	}
	() as PacketSink = FileSink(PacketStream) { param file: "debug.TestPacketFileSourceBasic1.PacketStream.out"; format: txt; hasDelayField: true; flush: 1u; }	

}

