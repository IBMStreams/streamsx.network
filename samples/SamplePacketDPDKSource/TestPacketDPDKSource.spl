/*
 * Copyright (C) 2015, International Business Machines Corporation
 * All Rights Reserved
 */

use com.ibm.streamsx.network.ipv4::*;
use com.ibm.streamsx.network.ipv6::*;
use com.ibm.streamsx.network.source::*;

/* 
 * Simple example application which configures the DPDP source and 
 * sends the packet data to a file.  In this sample, we configure ingest
 * to run on 1 NIC port with 2 queues.
 */

type PacketData = uint64 timestampMicroseconds,
     blob   packet,
     uint64 length;

public composite TestPacketDPDKSource {   
	graph   
        // One DPDK master operator + one operator per NIC port/queue.  In this
        // example we have 1 port with 2 queues, so 3 PacketDPDKSource operators are 
        // required.

        // This is the master PacketDPDKSource.  It configures the overall DPDK
        // subsystem and does runtime management but does not produce a packet stream.
        (stream<PacketData>  PacketStream_NULL ) = PacketDPDKSource() {
            param
                // Parameters for the DPDK environment on this host.
                coreMask   : "0x7"; // 3 cores (0,1,2) required. One master, one for each queue.
                portMask   : "0x1"; // Only NIC port 0 will be utilized.
                numQueues  : 2;     // Utilize 2 queues on the NIC.
                isMaster   : true;  // This is the master DPDK operator.

                // Parameters for this specific operator.
                lcore      : 0;     // Run the master operator on core 0.
        } 

        (stream<PacketData>  PacketStream_0 ) = PacketDPDKSource() {
            param
                // Parameters for this specific operator.
                lcore   : 1;     // Run this ingest operator on core 1.
                nicPort : 0;     // Bind to NIC port 0
                nicQueue: 0;     // Bind to NIC queue 0
                promiscuous: true;
                metricsInterval: 1.0;
            output
                PacketStream_0:
                    timestampMicroseconds = (uint64) CAPTURE_MICROSECONDS(),
                    packet = PACKET_DATA(),
                    length = (uint64) PACKET_LENGTH();
        }

        (stream<PacketData>  PacketStream_1 ) = PacketDPDKSource() {
            param
                // Parameters for this specific operator.
                lcore   : 2;     // Run this ingest operator on core 2.
                nicPort : 0;     // Bind to NIC port 0
                nicQueue: 1;     // Bind to NIC queue 1
                promiscuous: true;
                metricsInterval: 1.0;
            output
                PacketStream_1:
                    timestampMicroseconds = (uint64) CAPTURE_MICROSECONDS(),
                    packet = PACKET_DATA(),
                    length = (uint64) PACKET_LENGTH();
        }

        () as FS_PacketData_0 = FileSink( PacketStream_0 ) {
            param
                file : "PacketStream0.out";
        }

        () as FS_PacketData_1 = FileSink( PacketStream_1 ) {
            param
                file : "PacketStream1.out";
        }

        // All of the PacketDPDKSource operators must run in the same PE (process).
	config
	    placement : partitionColocation('SinglePE');
	    tracing   : trace;
}


